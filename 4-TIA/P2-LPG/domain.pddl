(define (domain asfaltado)
	(:requirements :durative-actions :typing :fluents)
	(:types tramo cuadrilla cisterna pavimentadora compactadora -object)

	(:predicates (at ?t - tramo ?x - (either cuadrilla cisterna pavimentadora compactadora) )
				(disponible ?x - (either tramo cuadrilla cisterna pavimentadora compactadora))
				(compactado ?t - tramo)
				(pavimentado ?t - tramo)
				(aplastado ?t - tramo)
				(sign ?t - tramo)
				(conectado ?t1 - tramo ?t2 - tramo)
				(pintado ?t - tramo)
				(vallas ?t - tramo)
				(need_sign ?t - tramo)

	)
	
	(:functions 
				(distance ?t1 - tramo ?t2 - tramo)
				(coste ?x - (either cisterna pavimentadora compactadora))
				(coste-total)
	)

	
	;Acciones de Mantenimiento
	(:durative-action compactado
	:parameters (?t - tramo)
	:duration (= ?duration 250)
	:condition (at start (disponible ?t))
	:effect (and (at start (not (disponible ?t)))
				 (at end (disponible ?t))	
				 (at end (compactado ?t))
			)
	)
	
	(:durative-action pavimentacion
	:parameters (?t - tramo ?c - cisterna ?p - pavimentadora)
	:duration (= ?duration 190)
	:condition (and (at start (disponible ?t))
					(at start (disponible ?c))
					(at start (disponible ?p))
					(over all (at ?t ?c))
					(over all (at ?t ?p))
					(over all (compactado ?t))
				)
	:effect (and (at start (not (disponible ?t)))
				(at start (not (disponible ?c)))
				(at start (not (disponible ?p)))
				(at end (pavimentado ?t))
				(at end (disponible ?t))
				(at end (disponible ?c))
				(at end (disponible ?p))
				(at end (increase (coste-total) (coste ?p)))
				(at end (increase (coste-total) (coste ?c)))
			)
	)

	(:durative-action aplastacion
	:parameters (?t - tramo ?c - compactadora)
	:duration (= ?duration 150)
	:condition (and (at start (disponible ?t))
					(at start (disponible ?c))
					(over all (at ?t ?c))
					(over all (pavimentado ?t))
				)
	:effect (and (at start (not (disponible ?t)))
				(at start (not (disponible ?c)))
				(at end (aplastado ?t))
				(at end (disponible ?t))
				(at end (disponible ?c))
				(at end (increase (coste-total) (coste ?c)))
			)
	)

	;Acciones finales requieren cuadrilla, disponible tramo

	(:durative-action pintado_marcas_viarias
	:parameters (?x - cuadrilla ?t - tramo)
	:duration (= ?duration 30)
	:condition (and (at start (disponible ?t))
					(at start (disponible ?x))
					(over all (at ?t ?x))
					(over all (aplastado ?t))
				)
	:effect (and (at start (not (disponible ?t)))
				(at start (not (disponible ?x)))
				(at end (pintado ?t))
				(at end (disponible ?t))
				(at end (disponible ?x))
			)
	)
	
	(:durative-action colocar_vallas_y_quitamiedos
	:parameters (?x - cuadrilla ?t - tramo)
	:duration (= ?duration 120)
	:condition (and (at start (disponible ?t))
					(at start (disponible ?x))
					(over all (at ?t ?x))
					(over all (aplastado ?t))
				)
	:effect (and (at start (not (disponible ?t)))
				(at start (not (disponible ?x)))
				(at end (vallas ?t))
				(at end (disponible ?t))
				(at end (disponible ?x))
			)
	)
	
	(:durative-action colocar_senyales_y_paneles_luminosos
	:parameters (?x - cuadrilla ?t - tramo)
	:duration (= ?duration 70)
	:condition (and (at start (disponible ?t))
					(at start (disponible ?x))
					(over all (at ?t ?x))
					(over all (aplastado ?t))
					(at start (need_sign ?t))
				)
	:effect (and (at start (not (disponible ?t)))
				(at start (not (disponible ?x)))
				(at start (not (need_sign ?t)))
				(at end (sign ?t))
				(at end (disponible ?t))
				(at end (disponible ?x))
			)
	)


	;Acciones complementarias

	(:durative-action transporte_maquinaria
	:parameters (?t1 ?t2 - tramo ?x - (either cisterna pavimentadora compactadora))
	:duration (= ?duration (* (distance ?t1 ?t2) 2))
	:condition (and (at start (at ?t1 ?x))
					(at start (conectado ?t1 ?t2))
					(at start (disponible ?x))
					(at start (disponible ?t1))
					(at start (disponible ?t2))
				)
	:effect (and 
				(at start (not (at ?t1 ?x)))
				(at end (at ?t2 ?x))
				(at start (not (disponible ?x)))
				(at end (disponible ?x))
				(at start (not (disponible ?t1)))
				(at end (disponible ?t1))
				(at start (not (disponible ?t2)))
				(at end (disponible ?t2))
			)
	)

	(:durative-action transporte_cuadrilla
	:parameters (?t1 ?t2 - tramo ?x - cuadrilla)
	:duration (= ?duration (distance ?t1 ?t2))
	:condition (and (at start (at ?t1 ?x))
					(at start (conectado ?t1 ?t2))
					(at start (disponible ?x))
					(at start (disponible ?t1))
					(at start (disponible ?t2))
				)
	:effect (and
				(at start (not (at ?t1 ?x)))
				(at end (at ?t2 ?x))
				(at start (not (disponible ?x)))
				(at end (disponible ?x))
				(at start (not (disponible ?t1)))
				(at end (disponible ?t1))
				(at start (not (disponible ?t2)))
				(at end (disponible ?t2))
			)
	)

)